# astrolabe.ppl
#
# The PyXPlot code in this file makes the various parts of a model astrolabe as
# described by Dominic Ford's paper in the Journal of the British Astronomical
# Association (2011).
#
# Copyright (C) 2010 Dominic Ford <dcf21@mrao.cam.ac.uk>
#
# $Id$
#
# This code is free software; you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free Software
# Foundation; either version 2 of the License, or (at your option) any later
# version.
#
# You should have received a copy of the GNU General Public License along with
# PyXPlot; if not, write to the Free Software Foundation, Inc., 51 Franklin
# Street, Fifth Floor, Boston, MA  02110-1301, USA

# ----------------------------------------------------------------------------

# Top level script for drawing the parts of the astrolabe

# assert version >=0.8.2

!mkdir -p output
!mkdir -p doc/tmp
!mkdir -p doc/output
!rm -f output/* doc/output/* doc/tmp/*

load "mother_front.ppl" # Render the front of the mother of the astrolabe
load "mother_back.ppl"  # Render the back of the mother of the astrolabe
load "rete.ppl"         # Render the rete of the astrolabe
load "rule.ppl"         # Render the rule and the alidade of the astrolabe

# Render climates for latitudes at 2-degree spacings from 8 deg -- 86 deg
for latitude=8 to 86.1 step 2
 {
  load "climate.ppl"

  foreach Ns in (1,0)
   {
    set term eps
    title="mother_front_combi"
    set multiplot
    set nodisplay
    clear
    eps "output/mother_front_north.eps"
    if (Ns) { f = "output/climate_%dN.eps"%(latitude); } \
    else    { f = "output/climate_%dS.eps"%(latitude); }
    eps f at D12*3+unit(mm),D12*3+unit(mm)
    load "end.ppl"

    !cp output/mother_back.eps doc/tmp
    !cp output/mother_front_combi.eps doc/tmp/mother_front.eps
    exec "!cp output/rete_%s.eps doc/tmp/rete.eps"%(conditionalS(Ns,"north","south"))
    exec "!cp output/rule_%s.eps doc/tmp/rule.eps"%(conditionalS(Ns,"north","south"))

    cd doc
    !latex astrolabe.tex
    !latex astrolabe.tex
    !latex astrolabe.tex
    !dvips astrolabe.dvi -o astrolabe.ps
    !ps2pdf astrolabe.ps astrolabe.pdf
    exec "!cp astrolabe.pdf output/astrolabe_%d%s.pdf"%(latitude,conditionalS(Ns,"N","S"))
    !rm *.aux *.log *.dvi *.ps *.pdf
    cd ..
   }
 }

