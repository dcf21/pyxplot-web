# mother_front.ppl
#
# The PyXPlot code in this file is part of an astrolabe
#
# Copyright (C) 2010 Dominic Ford <dcf21@mrao.cam.ac.uk>
#
# $Id$
#
# PyXPlot is free software; you can redistribute it and/or modify it under the
# terms of the GNU General Public License as published by the Free Software
# Foundation; either version 2 of the License, or (at your option) any later
# version.
#
# You should have received a copy of the GNU General Public License along with
# PyXPlot; if not, write to the Free Software Foundation, Inc., 51 Franklin
# Street, Fifth Floor, Boston, MA  02110-1301, USA

# ----------------------------------------------------------------------------

# The front of the mother of the astrolabe

set multiplot
set nodisplay
load "settings.ppl"
set fontsize 1.2*FS

# Radii of circles to be drawn on the mother
R2 = R1 - D12 * 1.5
R3 = R2 - D12
R4 = R3 - D12/2
R5 =      D12*CENTRE_SCALING

# Handle on top of astrolabe
circle at 0,R1 radius 2*unit(cm)
line from 0,R1-2*unit(cm) to 0,R1+2*unit(cm)

# Draw circles onto mother
circle at 0,0 radius R1 w fillc white
circle at 0,0 radius R2
circle at 0,0 radius R3
arc    at 0,0 radius R4 from TabSize to 360*unit(deg)-TabSize
circle at 0,0 radius R5

for theta=5*unit(deg) to 359*unit(deg) step 5*unit(deg)
 {
  line from R2*sin(theta),R2*cos(theta) to R4*sin(theta),R4*cos(theta)
 }

for theta=TabSize to 360.9*unit(deg)-TabSize step 1*unit(deg)
 {
  line from R3*sin(theta),R3*cos(theta) to R4*sin(theta),R4*cos(theta)
 }

RT1 = (R2+R3)/2
set textvalign centre
for theta=-180*unit(deg) to 170*unit(deg) step 10*unit(deg)
 {
  if      (theta<-179*unit(deg)) { theta_disp = theta               ; } \
  else if (theta<- 90*unit(deg)) { theta_disp = theta+180*unit(deg) ; } \
  else if (theta<   0*unit(deg)) { theta_disp =-theta               ; } \
  else if (theta<  90*unit(deg)) { theta_disp = theta               ; } \
  else                          { theta_disp =-theta+180*unit(deg)  ; }
  theta_disp = floor(theta_disp/unit(deg) + 0.01)

  if (theta_disp == 0)
   {
    set texthalign centre ; theta2 = theta
    text "0" at RT1*cos(theta2),RT1*sin(theta2) rot theta+90*unit(deg)
   } \
  else if (theta_disp == -180)
   {
    set texthalign centre ; theta2 = theta
    text "\LARGE\kreuz" at RT1*cos(theta2),RT1*sin(theta2) rot theta+90*unit(deg)
   } \
  else
   {
    set texthalign right ; theta2 = theta-0.2*unit(deg)
    text "%d"%(theta_disp/10) at RT1*cos(theta2),RT1*sin(theta2) rot theta+90*unit(deg)
    set texthalign left ; theta2 = theta+0.2*unit(deg)
    text "%d"%(theta_disp%10) at RT1*cos(theta2),RT1*sin(theta2) rot theta+90*unit(deg)
   }
 }

RT1 = (R1+R2)/2
set texthalign centre ; set textvalign centre
set fontsize 2*FS
i=0
foreach t in ("\LARGE\kreuz","A","B","C","D","E","F","G","H","I","K","L","M","N","O","P","Q","R","S","T","U","X","Y","Z")
 {
  theta = i/24*unit(rev)
  text "%s"%(t) at RT1*sin(theta),RT1*cos(theta) rot -theta
  i=i+1
 }

# Finish up
title = "mother_front"
load "end.ppl"

