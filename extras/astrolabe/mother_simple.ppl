# BACK OF MOTHER
# $Id$

set mult ; set nodisp
set preamble "\usepackage{wasysym}"
load "settings.ppl"
set fontsize 1.2*FS

R2 = R1 - D12
R3 = R2 - D12/2
R4 = R3 - D12
R5 = R4 - D12
R6 = R5 - D12
R7 = R6 - D12
R8 = R7 - D12/2
R9 = R8 - D12
#R10= R9 - D12
#R11= R10- D12
#R12= R11- D12
R13=      D12*CENTRE_SCALING

# Handle
circle at 0,R1 radius 2*unit(cm)
line from 0,R1-2*unit(cm) to 0,R1+2*unit(cm)

circle at 0,0 radius R1 w fillc white
circle at 0,0 radius R2
circle at 0,0 radius R3 with lw 2
circle at 0,0 radius R4
circle at 0,0 radius R5 with lw 2
circle at 0,0 radius R6
circle at 0,0 radius R7
circle at 0,0 radius R8
circle at 0,0 radius R9
#circle at 0,0 radius R10 with lw 2
#circle at 0,0 radius R11
#circle at 0,0 radius R12
circle at 0,0 radius R13

for theta=0*unit(deg) to 359*unit(deg) step 30*unit(deg)
 {
  line from R1*cos(theta),R1*sin(theta) to R5*cos(theta),R5*sin(theta)
 }

for theta=0*unit(deg) to 359*unit(deg) step 5*unit(deg)
 {
  line from R1*cos(theta),R1*sin(theta) to R4*cos(theta),R4*sin(theta)
 }

for theta=0*unit(deg) to 359.9*unit(deg) step 1*unit(deg)
 {
  line from R2*cos(theta),R2*sin(theta) to R3*cos(theta),R3*sin(theta)
 }

for theta=0*unit(rev) to 1*unit(rev) step 1/365*unit(rev)
 {
  line from R7*cos(theta),R7*sin(theta) to R8*cos(theta),R8*sin(theta)
 }

RT1 = (R1+R2)/2
RT2 = (R3+R4)/2
set textvalign centre
for theta=-180*unit(deg) to 170*unit(deg) step 10*unit(deg)
 {
  if      (theta<-179*unit(deg)) { theta_disp = theta               ; } \
  else if (theta<- 90*unit(deg)) { theta_disp = theta+180*unit(deg) ; } \
  else if (theta<   0*unit(deg)) { theta_disp =-theta               ; } \
  else if (theta<  90*unit(deg)) { theta_disp = theta               ; } \
  else                          { theta_disp =-theta+180*unit(deg)  ; }
  theta_disp = floor(theta_disp/unit(deg) + 0.01)

  if (theta_disp == 0)
   {
    set texthalign centre ; theta2 = theta
    text "0" at RT1*cos(theta2),RT1*sin(theta2) rot theta+90*unit(deg)
   } \
  else if (theta_disp == -180)
   {
    set texthalign centre ; theta2 = theta
    text "\LARGE\kreuz" at RT1*cos(theta2),RT1*sin(theta2) rot theta+90*unit(deg)
   } \
  else
   {
    set texthalign right ; theta2 = theta-0.2*unit(deg)
    text "%d"%(theta_disp/10) at RT1*cos(theta2),RT1*sin(theta2) rot theta+90*unit(deg)
    set texthalign left ; theta2 = theta+0.2*unit(deg)
    text "%d"%(theta_disp%10) at RT1*cos(theta2),RT1*sin(theta2) rot theta+90*unit(deg)
   }

  theta_disp = floor(theta/unit(deg) + 380.01) % 30 + 10
  set texthalign right ; theta2 = theta-0.2*unit(deg)
  text "%d"%(theta_disp/10) at RT2*cos(theta2),RT2*sin(theta2) rot theta+90*unit(deg)
  set texthalign left ; theta2 = theta+0.2*unit(deg)
  text "%d"%(theta_disp%10) at RT2*cos(theta2),RT2*sin(theta2) rot theta+90*unit(deg)
 }

RT1 = (R6+R7)/2
RT2 = (R8+R9)/2
foreach datum m,d,last,z1394,a1394,z1974,a1974 in "tuckerman.dat"
 {
  theta = 30*unit(deg)*(z1974-1) + a1974*unit(deg)
  if (last) { line from R5*cos(theta),R5*sin(theta) to R7*cos(theta),R7*sin(theta) ; } \
  else      { line from R6*cos(theta),R6*sin(theta) to R7*cos(theta),R7*sin(theta) ; }

  if (((d%10)==0) | (d>26))
   {
    set texthalign right ; theta2 = theta-0.2*unit(deg)
    text "%d"%(d/10) at RT1*cos(theta2),RT1*sin(theta2) rot theta+90*unit(deg)
    set texthalign left ; theta2 = theta+0.2*unit(deg)
    text "%d"%(d%10) at RT1*cos(theta2),RT1*sin(theta2) rot theta+90*unit(deg)
   }

#  theta = 30*unit(deg)*(z1394-1) + a1394*unit(deg)
#  if (last) { line from R8*cos(theta),R8*sin(theta) to R10*cos(theta),R10*sin(theta) ; } \
#  else      { line from R8*cos(theta),R8*sin(theta) to R9 *cos(theta),R9 *sin(theta) ; }

#  if (((d%10)==0) | (d>26))
#   {
#    set texthalign right ; theta2 = theta-0.2*unit(deg)
#    text "%d"%(d/10) at RT2*cos(theta2),RT2*sin(theta2) rot theta+90*unit(deg)
#    set texthalign left ; theta2 = theta+0.2*unit(deg)
#    text "%d"%(d%10) at RT2*cos(theta2),RT2*sin(theta2) rot theta+90*unit(deg)
#   }
 }

RT1 = (R4+R5 )/2
RT2 = (R5+R6 )/2
#RT3 = (R9+R10)/2
load "mother_back_text_simple.dat"

# White out R13
circle at 0,0 radius R13 w fillc white

set term eps
set output 'mother_back_simple.eps'
set disp ; refresh
set term pdf
set output 'mother_back_simple.pdf'
set disp ; refresh



# FRONT OF MOTHER

set mult ; set nodisp ; clear
set fontsize 1.2*FS
R2 = R1 - D12 * 1.5
R3 = R2 - D12
R4 = R3 - D12/2
R5 =      D12*CENTRE_SCALING

# Handle
circle at 0,R1 radius 2*unit(cm)
line from 0,R1-2*unit(cm) to 0,R1+2*unit(cm)
  
circle at 0,0 radius R1 w fillc white
circle at 0,0 radius R2
circle at 0,0 radius R3
arc    at 0,0 radius R4 from TabSize to 360*unit(deg)-TabSize
circle at 0,0 radius R5

for theta=5*unit(deg) to 359*unit(deg) step 5*unit(deg)
 {
  line from R2*sin(theta),R2*cos(theta) to R4*sin(theta),R4*cos(theta)
 }

for theta=TabSize to 360.9*unit(deg)-TabSize step 1*unit(deg)
 {
  line from R3*sin(theta),R3*cos(theta) to R4*sin(theta),R4*cos(theta)
 }

RT1 = (R2+R3)/2
set textvalign centre
for theta=-180*unit(deg) to 170*unit(deg) step 10*unit(deg)
 {
  if      (theta<-179*unit(deg)) { theta_disp = theta               ; } \
  else if (theta<- 90*unit(deg)) { theta_disp = theta+180*unit(deg) ; } \
  else if (theta<   0*unit(deg)) { theta_disp =-theta               ; } \
  else if (theta<  90*unit(deg)) { theta_disp = theta               ; } \
  else                          { theta_disp =-theta+180*unit(deg)  ; }
  theta_disp = floor(theta_disp/unit(deg) + 0.01)

  if (theta_disp == 0)
   {
    set texthalign centre ; theta2 = theta
    text "0" at RT1*cos(theta2),RT1*sin(theta2) rot theta+90*unit(deg)
   } \
  else if (theta_disp == -180)
   {
    set texthalign centre ; theta2 = theta
    text "\LARGE\kreuz" at RT1*cos(theta2),RT1*sin(theta2) rot theta+90*unit(deg)
   } \
  else
   {
    set texthalign right ; theta2 = theta-0.2*unit(deg)
    text "%d"%(theta_disp/10) at RT1*cos(theta2),RT1*sin(theta2) rot theta+90*unit(deg)
    set texthalign left ; theta2 = theta+0.2*unit(deg)
    text "%d"%(theta_disp%10) at RT1*cos(theta2),RT1*sin(theta2) rot theta+90*unit(deg)
   }
 }

RT1 = (R1+R2)/2
set texthalign centre ; set textvalign centre
set fontsize 2*FS
i=0
foreach t in ("\LARGE\kreuz","13","14","15","16","17","18","19","20","21","22","23","\footnotesize Midnight","1","2","3","4","5","6","7","8","9","10","11")
 {
  theta = i/24*unit(rev)
  text "%s"%(t) at RT1*sin(theta),RT1*cos(theta) rot -theta
  i=i+1
 }

set term eps
set output 'mother_front_simple.eps'
set disp ; refresh
set term pdf
set output 'mother_front_simple.pdf'
set disp ; refresh

