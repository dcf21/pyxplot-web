# THE PLATE

set mult ; set nodisp
set preamble "\usepackage{wasysym}"
load "settings.ppl"
set fontsize 0.8*FS

Rtab = R1 - D12*2.5 - unit(mm)
R2   = R1 - D12*3 - unit(mm)
R3   =      D12

R4 = R2 * tan((90-iEarth)/2*unit(deg))
R5 = R4 * tan((90-iEarth)/2*unit(deg))

circle at 0,0 radius R2
circle at 0,0 radius R4
circle at 0,0 radius R5

# Make tab
arc at 0,0 radius Rtab from -TabSize to TabSize
line from  Rtab*sin(TabSize),Rtab*cos(TabSize) to  R2*sin(TabSize),R2*cos(TabSize)
line from -Rtab*sin(TabSize),Rtab*cos(TabSize) to -R2*sin(TabSize),R2*cos(TabSize)

# Lines of constant altitude
foreach c in (-6,0,5,10,15,20,25,30,35,40,45,50,55,60,65,70,75,80,85)
 {
  theta1 = (-latitude-(90-c))*unit(deg)
  theta2 = (-latitude+(90-c))*unit(deg)

  x1 = R4 * sin(theta1) ; y1 = R4 * cos(theta1)
  x2 = R4 * sin(theta2) ; y2 = R4 * cos(theta2)

  yA = y1 * (R4 / (R4-x1))
  yB = y2 * (R4 / (R4-x2))

  # Record centre and radius of the horizon arc
  if (c==0)
   {
    Hcentre = (yA+yB)/2
    Hradius = (yB-yA)/2
   }

  if (yB < R2)
   {
    start = 0
    end = 360
    if ((c%10)==0)
     {
      text "%d"%(c) at 0,yB hal c val b
     }
   } else {
    r = (yB-yA)/2
    y = (yA+yB)/2
    start = 180*unit(deg) - acos( (r**2 + y**2 - R2**2 ) / (2*((yB-yA)/2)*((yA+yB)/2)))
    end   = -start
    if ((c%10)==0)
     {
      text "%d"%(c) at r*sin(start+unit(deg)),(yA+yB)/2+r*cos(start+unit(deg)) hal r val t gap unit(mm)
      text "%d"%(c) at r*sin(end  -unit(deg)),(yA+yB)/2+r*cos(end  -unit(deg)) hal l val t gap unit(mm)
     }
   }

  arc at 0,(yA+yB)/2 radius (yB-yA)/2 from start to end with linetype 1+(c<0) linewidth 0.5+1.5*(c==0)
 }

# Find coordinates of P
theta = -latitude*unit(deg)
Px    =  R4 * sin(theta)
Py    =  R4 * cos(theta)

# Find coordinates of Z
Zx    =  0*unit(m)
Zy    =  0*unit(m) + Py / (R4-Px) * R4

# Find midpoint between Z and H
ZHx = -R4/2
ZHy = Zy/2

# Find bearing of T from ZH (clockwise from right-going axis)
theta = atan2(Zx-(-R4),Zy-0*unit(m))

# Find coordinates of T
Tx = 0*unit(m)
Ty = ZHy + ZHx * tan(theta)

# Lines of constant azimuth
ss = 11.25*unit(deg)
c=-90*unit(deg)+ss
foreach dir in ("","NNW","","NW","","WNW","","W","","WSW","","SW","","SSW","")
 {
  Tx = -Ty * tan(c)
  Tr = hypot(Tx , Ty-Zy) # Radius of arc of constant azimuth

  t_hc = hypot(Tx , Ty-Hcentre) # Distance from T to centre of horizon
  theta = acos( (Tr**2+t_hc**2-Hradius**2) / (2*Tr*t_hc) )
  phi   = atan2(Tx-0*unit(m) , Hcentre-Ty)
  start = -phi-theta
  end   = -phi+theta

  t_c = hypot(Tx , Ty) # Distance from T to centre of the astrolabe
  theta = acos( (Tr**2+t_c**2-R2**2) / (2*Tr*t_c) )
  phi   = atan2(Tx , -Ty)
  start2= -phi-theta
  end2  = -phi+theta

  arc at Tx,Ty radius Tr from max(start,start2) to min(end,end2) with lw 0.5

  if (end < end2-unit(2*deg))
   {
    text "%s"%(dir) at Tx+Tr*sin(end),Ty+Tr*cos(end) hal c val t gap unit(mm) rot 90*unit(deg)-end
   } else {
    text "%s"%(dir) at Tx+Tr*sin(end2-unit(3*deg)),Ty+Tr*cos(end2-unit(3*deg)) hal r val b gap unit(mm) rot -(end-unit(3*deg))
   }

  dir =~ s/N/s/g
  dir =~ s/W/E/g
  dir =~ s/S/N/g
  dir =~ s/s/S/g

  if (start > start2+unit(2*deg))
   {
    text "%s"%(dir) at Tx+Tr*sin(start),Ty+Tr*cos(start) hal c val t gap unit(mm) rot -90*unit(deg)-start
   } else {
    text "%s"%(dir) at Tx+Tr*sin(start2+unit(3*deg)),Ty+Tr*cos(start2+unit(3*deg)) hal l val b gap unit(mm) rot -(start+unit(3*deg))
   }

  c = c + ss
 }

text "N" at 0,Hcentre-Hradius hal c val t gap unit(mm)

# White out R3
line from -R2,0 to R2,0
line from 0,-R2 to 0,R2
circle at 0,0 radius R3 w fillc white

set term eps
set output 'plate.eps'
set disp ; refresh
set term pdf
set output 'plate.pdf'
set disp ; refresh

